Haskell: 遅延型を利用したメモ化

<p>(工事中 70%)</p>

<h2>このページでは</h2>

<p>
このページでは以下のような内容を扱う。
</p>

<p>
単純な再帰的関数では非常に時間がかかってしまう処理を
遅延型を利用したメモ化を使うことで効率的に実行することができる。
そのような手法を「単純な機械の例」を使って説明する。
素直な実装では200億年かかる演算がメモ化を使うことで0.2秒で終了することを示す。
</p>

<h2>弱頭部正規形までの評価</h2>

<p>
Haskellでは代数的データ型の中身についても、
必要になるまでは評価しないという特徴がある。
正式に言うと弱頭部正規形までの評価ということになる。
これを利用することで効率的なメモ化を簡潔なコードで行うことが可能になる。
この手法はパックラット構文解析に使用されている。
</p>

<h2>メモ化のために必要な条件</h2>

<p>
メモ化するためにはメモリ上の表現として同一である必要がある。
たとえば、以下のような場合にはg 3は一度しか実行されず、
ある意味ではメモ化されていると言うことができる。
</p>

<pre><code>let f x = x + x in f $ g 3</code></pre>

<p>
しかし、以下の場合には1回目のg 3と2回目のg 3とは別々に評価される。
つまりメモ化されていないと言える。
</p>

<pre><code>g 3 + g 3</code></pre>

<p>
つまり、関数の引数が同一であったとしても、
もともとの表記上で同一でなければ、その都度別々に評価される。
</p>

<h2>単純な例</h2>

<p>
フィボナッチ数列の素直な定義として以下のようなものがある。
</p>

<pre><code>fib 0 = 0
fib 1 = 1
fib n = fib (n - 2) + fib (n - 1)</code></pre>

<p>
しかし、これは評価に指数関数時間が必要となる。
これを以下のように書き換えることができる。
</p>

<pre><code>fib = (fibs !!)
fibs@(_ : tfibs) = 0 : 1 : zipWith (+) fibs tfibs</code></pre>

<p>
これはメモ化による最適化の一例である。
</p>

<h2>バックトラック</h2>

<p>
何かの作業がうまくいかなかったときに、すこしもどって、
もう一度やりなおすといったアルゴリズムがある。
たとえばある場所に行きたいとき、
ある道順で行って行き止まりになってしまったとする。
そしたらその直前の交差点までもどり別の道を行く。
その交差点から行けるすべての道を試してもたどりつけなかったならば、
さらに前の交差点までもどり同じことをくりかえす。
これがバックトラックである。
</p>

<p>
全ての交差点で道が2又に分かれている道を考える。
すると試す回数は、目的地までの距離(交差点の数)をnとして、
だいたい2^n程度になる。
つまり、バックトラックを愚直に実装すると最悪の場合、指数関数時間が
かかってしまうことになる。
</p>

<h2>入力列から値を作成する機械の例</h2>

<p>
入力列をたどりながら値を作成する機械を考える。
まずは数字の列を取り、それに続いて'+'または'-'の文字列を取る。
結果は数字の列と逆順に'+'または'-'を適用していったものとする。
たとえば"3456+-++"ならば3(4(5(6+)-)+)+のように解釈され、結果は
0+6-5+4+3で8になる。
これはスタックマシンを使えば効率的に実装できる。
</p>

<h3>再帰的処理による実装</h3>

<p>
ここではあえて再帰的処理によって実装し、
それが指数関数時間になることを見ていこう。
</p>

<p>
<a href="/computer/haskell/algorithm/memoization/lazy/machine_rec.hs"
	>machine_rec.hs</a>
(<a
href="/computer/haskell/algorithm/memoization/lazy/code_ex_machine_rec.html"
id="link_to_code_ex_machine_rec"
	>コード解説</a>)
</p>

<pre><code>type Result v = Maybe (v, String)</code></pre>

<pre><code>char :: String -&gt; Result Char
char (x : xs) = Just (x, xs)
char _ = Nothing</code></pre>

<pre><code>run :: String -&gt; Maybe Int
run src | Just (n, _) &lt;- rule src = Just n
run _ = Nothing</code></pre>

<pre><code>rule :: String -&gt; Result Int
rule s0 = msum [ do
	(c, s) &lt;- char s0
	guard $ isDigit c
	(n, s') &lt;- rule s
	('+', s'') &lt;- char s'
	return (n + fromDigit c, s''), do
	(c, s) &lt;- char s0
	guard $ isDigit c
	(n, s') &lt;- rule s
	('-', s'') &lt;- char s'
	return (n - fromDigit c, s''),
	return (0, s0) ]</code></pre>

<pre><code>fromDigit :: Char -&gt; Int
fromDigit '0' = 0
fromDigit '1' = 1
...
fromDigit '9' = 9
fromDigit c = error $ show c ++ " is not digit."</code></pre>

<p>
サンプル入力作成用の関数を作成して試してみる。
</p>

<pre><code>sample n = take n (concat $ repeat "0123456789")
	++ replicate n '-'</code></pre>

<pre><code>% time ghc -e 'run $ sample 0' machine_rec.hs
Just 0
ghc -e 'run $ sample 0' machine_rec.hs 0.20s user 0.01s
system 94% cpu 0.224 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 16' machine_rec.hs
Just (-60)
ghc -e 'run $ sample 16' machine_rec.hs 0.74s user 0.02s
system 90% cpu 0.841 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 17' machine_rec.hs
Just (-66)
ghc -e 'run $ sample 17' machine_rec.hs 1.29s user 0.02s
system 93% cpu 1.406 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 18' machine_rec.hs
Just (-73)
ghc -e 'run $ sample 18' machine_rec.hs 2.40s user 0.02s
system 90% cpu 2.686 total</code></pre>

<p>実行時間を表にすると以下のようになる。</p>

<table border="1">
<tr><th>数字の列の長さ</th><th>実行時間</th><th>長さ0との差</th></tr>
<tr><td>0</td><td>0.20s</td><td>0s</td></tr>
<tr><td>16</td><td>0.74s</td><td>0.54s</td></tr>
<tr><td>17</td><td>1.29s</td><td>1.09s</td></tr>
<tr><td>18</td><td>2.40s</td><td>2.20s</td></tr>
<tr><td>...</td><td>...</td><td>...</td></tr>
<tr><td>76</td><td>200億年</td><td>200億年</td></tr>
</table>

<p>
数字の列の長さが1増えると実行時間が2倍になっているのがわかる。
つまりO(2^n)であり、指数関数時間がかかっているということになる。
なお、長さ76のデータは理論値である。
宇宙が始まったのが138億年前と言われている
(<a href="https://ja.wikipedia.org/wiki/宇宙の年表"
>ウィキペディア:宇宙の年表</a>参照)。
</p>

<h3>メモ化による効率化</h3>

<p>
<a
href="/computer/haskell/algorithm/memoization/lazy/machine_memo.hs"
	>machine_memo.hs</a>
(<a
href="/computer/haskell/algorithm/memoization/lazy/code_ex_machine_memo.html"
id="link_to_code_ex_machine_memo"
	>コード解説</a> 工事中 40%)
</p>

<pre><code>type Result v = Maybe (v, Derivs)</code></pre>

<pre><code>data Derivs = Derivs {
	rule :: Result Int,
	char :: Result Char }</code></pre>

<pre><code>run :: String -&gt; Maybe Int
run src | Just (n, _) &lt;- rule $ derivs src = Just n
run _ = Nothing</code></pre>

<pre><code>derivs :: String -&gt; Derivs
derivs src = d
	where
	d = Derivs rl ch
	rl = pRule d
	ch = case src of
		c : cs -&gt; Just (c, derivs cs)
		_ -&gt; Nothing</code></pre>

<pre><code>pRule :: Derivs -&gt; Result Int
pRule d0 = msum [ do
	(c, d) &lt;- char d0
	guard $ isDigit c
	(n, d') &lt;- rule d
	('+', d'') &lt;- char d'
	return (n + fromDigit c, d''), do
	(c, d) &lt;- char d0
	guard $ isDigit c
	(n, d') &lt;- rule d
	('-', d'') &lt;- char d'
	return (n - fromDigit c, d''),
	return (0, d0) ]</code></pre>

<p>試してみる。</p>

<pre><code>% time ghc -e 'run $ sample 0' machine_memo.hs
Just 0
ghc -e 'run $ sample 0' machine_memo.hs 0.20s user 0.02s
system 96% cpu 0.224 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 76' machine_memo.hs
Just (-330)
ghc -e 'run $ sample 76' machine_memo.hs 0.21s user 0.01s
system 95% cpu 0.228 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 100000' machine_memo.hs
Just (-450000)
ghc -e 'run $ sample 100000' machine_memo.hs 1.12s user 0.04s
System 90% cpu 1.283 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 200000' machine_memo.hs
Just (-900000)
ghc -e 'run $ sample 200000' machine_memo.hs 2.10s user 0.05s
System 87% cpu 2.452 total</code></pre>

<pre><code>% time ghc -e 'run $ sample 400000' machine_memo.hs
Just (-1800000)
ghc -e 'run $ sample 400000' machine_memo.hs 3.97s user 0.09s
System 83% cpu 4.870 total</code></pre>

<table border="1">
<tr><th>数字の列の長さ</th><th>実行時間</th><th>長さ0との差</th></tr>
<tr><td>0</td><td>0.20s</td><td>0s</td></tr>
<tr><td>76</td><td>0.21s</td><td>0.01s</td></tr>
<tr><td>100000</td><td>1.12s</td><td>0.92s</td></tr>
<tr><td>200000</td><td>2.10s</td><td>1.90s</td></tr>
<tr><td>400000</td><td>3.97s</td><td>3.77s</td></tr>
<tr><td>...</td><td>...</td><td>...</td></tr>
<tr><td>1億</td><td>15分</td><td>15分</td></tr>
</table>

<p>
長さ1億のデータは理論値である。
メモ化なしでは200億年かかる演算がメモ化することにより、0.2秒で終了する。
長さ10万についての計算が1秒で終了し、長さ40万については4秒で終了する。
予想通りO(n)、つまり線形時間で終了するようになった。
</p>

<p><a href="/computer/haskell/algorithm/memoization"
	>「メモ化」トップへもどる</a></p>
