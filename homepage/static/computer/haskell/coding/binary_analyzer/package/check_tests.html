バイナリ解析器: テスト用のファイルの見直し

<h2>動機</h2>

<p>
バイナリ解析器のテストのためにASN.1のDER方式の簡単なデコーダを作成した。
テストや用例として良い素材なので、読みやすくして、Analyzerパッケージの
exampleとして付属させたい。
十分短くできたなら.cabalファイルのdescriptionフィールドにも使えるかもしれない。
</p>

<p>
また、デコード例のためのDER形式の証明書についても、
作りかた等をリンクで示す等したい。
</p>

<h2>テスト用の証明書</h2>

<p>
<a href="/computer/web/rfc/x_690/example/test_ASN_1_cert.der"
>test_ASN_1_cert.der</a>(<a
href="/computer/web/rfc/x_690/example/test_ASN_1_cert.der.hex.txt"
>16進ダンプ</a>)
</p>

<p>
<a href="/computer/web/rfc/x_690/example"
>「X.690: テスト用ファイルの作成」</a>参照。
</p>

<h2>テストコード</h2>

<p>
<a href="/computer/haskell/coding/binary_analyzer/package/testAnalyzer.hs"
>testAnalyzer.hs</a>
</p>

<h3>インポート部分</h3>

<p>インポートリストを明記する。</p>

<h3>モジュールを分割</h3>

<p>
DERデコードライブラリの部分と、そのテストの部分とにモジュールを分割する。
</p>

<ul>
<li><a href="/computer/haskell/coding/binary_analyzer/package/Der.hs"
	>Der.hs</a></li>
<li><a href="/computer/haskell/coding/binary_analyzer/package/testDer.hs"
	>testDer.hs</a></li>
</ul>

<h2>Der.hs</h2>

<h3>エクスポートリスト</h3>

<pre><code>module Der (
	Asn1(..), Asn1Tag(..), TagClass(..), DataClass(..),
	decode ) where</code></pre>

<p>
デコード関数とデコードで返される値の型をエクスポートしている。
runAnalyzerもエクスポートしても良いと思うが、
ここでは使用者側でAnalyzerモジュールをインポートしてもらうことにした。
</p>

<h3>インポートリスト</h3>

<p>特に問題ない。</p>

<h3>データ型の定義</h3>

<pre><code>data Asn1
	= Asn1Cnst Asn1Tag [Asn1]
	| Asn1Prim Asn1Tag BS.ByteString
	deriving Show</code></pre>

<p>
比較的単純な樹構造のデータ型である。
タグが付いている以外にはAsn1Cnstが節であり、
Asn1Primが葉であるような樹構造。
ちなみにAsn1Cnstは「ASN.1 Constructed」で
Asn1Primは「ASN.1 Primitive」ということ。
</p>

<pre><code>data Asn1Tag
	= Asn1Tag TagClass DataClass Integer
	deriving Show</code></pre>

<p>
TagClassは4種類のうちどのクラスのタグかを指定する。
DataClassはデータ部が再帰的にASN.1の構造を取っているか、
単純なデータであるかを指定する。
Integerはタグ番号であり、この番号でデータの型が指定される。
タグ番号を表すデータ型を作成するか、型シノニムを定義しても良かったが、
今回は省略した。
</p>

<p>
TagClassとDataClassは単純な列挙型。
Enum型のインスタンスにしておくと後で便利。
</p>

<h3>デコード関数の定義</h3>

<h4>全体</h4>

<pre><code>decode :: Analyzer BS.ByteString [Asn1]
decode = listAll $ decodeTag &gt;&gt;= \t@(Asn1Tag _ dc _) -&gt;
	case dc of
		Primitive -&gt; Asn1Prim t &lt;$&gt; decodePrim
		Constructed -&gt; Asn1Cnst t &lt;$&gt; decodeCnst</code></pre>

<p>
タグのDataClassの値によって再帰的にデコードするかどうかを決めている。
decodePrimitiveとdecodeConstructedをそれぞれdecodePrim, decodeCnstと省略表記にした。
</p>

<h4>タグ</h4>

<pre><code>decodeTag :: Analyzer BS.ByteString Asn1Tag
decodeTag = decodeTag1 &gt;&gt;= \(tc, dc, mtn) -&gt; case mtn of
	Just tn -&gt; return . Asn1Tag tc dc $ fromIntegral tn
	_ -&gt; Asn1Tag tc dc &lt;$&gt; decodeTagR 0</code></pre>

<p>
decodeTag1の返すタプルの3番目の要素はJust値ならばそのまま型番号を表す。
Nothingであればさらに読み込む必要があることを意味するので、
その場合にはdecodeTagRで型番号を読み込むことになる。
</p>

<pre><code>decodeTag1 :: Analyzer
	BS.ByteString (TagClass, DataClass, Maybe Word8)
decodeTag1 = (&lt;$&gt; token) $ \w -&gt; (
	toEnum . fromEnum $ w `shiftR` 6,
	toEnum . fromEnum $ testBit w 5,
	checkMaybe (/= 0x1f) $ w .&amp;. 0x1f)</code></pre>

<pre><code>checkMaybe :: (a -&gt; Bool) -&gt; a -&gt; Maybe a
checkMaybe p x = if p x then Just x else Nothing</code></pre>

<p>
decodeTag1は1バイト目を取ってきて、それをTagClass、DataClass、
タグ番号に分解する解析器。
変数wはWord8の値なのでwにしたが、もっといい名前があるかもしれない。
</p>

<pre><code>decodeTagR :: Integer -&gt; Analyzer BS.ByteString Integer
decodeTagR n = token &gt;&gt;= \w -&gt; if testBit w 7
	then decodeTagR $
		n `shiftL` 7 .|. fromIntegral (w .&amp;. 0x7f)
	else return $ n `shiftL` 7 .|. fromIntegral w</code></pre>

<p>
decodeTagRのRは残り(rest)ということ。
nを蓄積変数として最上位ビットが1である限り次々に値を足していく。
値を足す前に7ビット左シフトすることで、ビッグエンディアンの数値として、
デコードできる。
</p>

<h4>データ</h4>

<pre><code>decodePrim :: Analyzer BS.ByteString BS.ByteString
decodePrim = decodeLength &gt;&gt;= tokens</code></pre>

<p>
primitiveなデータのデコードは単に得た長さ分のByteStringを取り出せば良い。
</p>

<pre><code>decodeCnst :: Analyzer BS.ByteString [Asn1]
decodeCnst = decodeLength &gt;&gt;= tokens &gt;&gt;= \bs -&gt;
	case runAnalyzer decode bs of
		Right (as, "") -&gt; return as
		Left err -&gt; fail err
		_ -&gt; error "Der.decodeCnst: never occur"</code></pre>

<p>
取り出したByteStringを再帰的にdecodeしている。
長さを指定して取り出したByteStringを明示的にデコードするためには、
解析器にrunAnalyzerを適用して関数を取り出す必要がある。
また、decodeはlistAllしているので、入力を消費しきらないで解析に成功することはない。
よって、エラーメッセージを"never occur"としている。
</p>

<pre><code>decodeLength :: Analyzer BS.ByteString Integer
decodeLength = token &gt;&gt;= \w -&gt; if testBit w 7
	then decodeLengthN (w .&amp;. 0x7f) 0
	else return $ fromIntegral w</code></pre>

<p>
1バイト読み出してその最上位ビットが1であれば、
桁数を指定して続きのバイト列をデータ長として読み出す。
そうでなければ、そのままの値がデータ長となる。
</p>

<pre><code>decodeLengthN ::
	Word8 -&gt; Integer -&gt; Analyzer BS.ByteString Integer
decodeLengthN n ln
	| n &gt;= 0x7f = fail $ "Der.decodeLengthN: " ++
		"the value 0b11111111 shall not be used"
	| n &lt;= 0 = return ln
	| otherwise = token &gt;&gt;= decodeLengthN (n - 1) .
		(ln `shiftL` 8 .|.) . fromIntegral</code></pre>

<p>
前半部分はエラーチェック。
後半は蓄積変数を8ビット左シフトした値に読み出した値を足している。
</p>

<h2>testDer.hs</h2>

<p><a href="/computer/haskell/coding/binary_analyzer/package/testDer.hs"
	>testDer.hs</a></p>

<p>
unsafePerformIOを利用して、test_ASN_1_cert.derの内容をcert変数に格納している。
</p>

<h2>テストしてみる</h2>

<pre><code>% runhaskell testDer.hs
Right ([Asn1Cnst (Asn1Tag Universal Constructed 16)
[Asn1Cnst (Asn1Tag Universal Constructed 16) [Asn1Cnst
(Asn1Tag ContextSpecific Constructed 0) [Asn1Prim (Asn1Tag
Universal Primitive 2) "\STX"],Asn1Prim (Asn1Tag Universal
Primitive 2) "\NUL\132h\196s9h\163\132",Asn1Cnst (Asn1Tag
<a
href="/computer/haskell/coding/binary_analyzer/package/test_result.txt"
>...</a>
\236\141\236\200h\156\&amp;2\\M\239\200\155\186\ACK\172"]],"")</code></pre>

<p>
<a href="/computer/haskell/coding/binary_analyzer/package/refactoring.html"
	>「リファクタリング」へもどる</a>
<a href="/computer/haskell/coding/binary_analyzer/package"
	>「パッケージの作成」トップへ</a>
</p>
