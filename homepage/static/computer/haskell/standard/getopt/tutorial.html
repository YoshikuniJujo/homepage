GetOptモジュール: チュートリアル

<h2>コマンドライン引数を入手する</h2>

<p>
解析する前にまずはコマンドライン引数を入手してみよう。
コマンドライン引数を入手するにはSystem.EnvironmentモジュールのgetArgsを使う。
</p>

<pre><code>getArgs :: IO [String]</code></pre>

<p>以下のようなファイルを試してみよう。</p>

<pre><code>% cat <a href="/computer/haskell/standard/getopt/testArgs.hs"
>testArgs.hs</a>
import System.Environment

main :: IO ()
main = do
	args &lt;- getArgs
	print args
% runhaskell testArgs.hs hello world
["hello", "world"]</code></pre>

<h2>OffとOn: 引数を持たないオプションの例</h2>

<p><a href="/computer/haskell/standard/getopt/testOpt.hs"
	>testOpt.hs</a></p>

<h3>仕様を決める</h3>

<p>
オプションでOffとOnという2つの値のどちらかを指定できるようにする。
Offは'-0'または'--off'で指定し、Onは'-1'または'--on'で指定する。
はじめの非オプション引数の後はすべて非オプションとして扱うものとする。
</p>

<h3>OffとOnを持つデータ型を定義</h3>

<p>
Bool型を使っても良いが、
意味をはっきりさせるためには独自のデータ型を定義したほうが良い。
</p>

<pre><code>data Switch = Off | On deriving Show</code></pre>

<h3>option関数</h3>

<p>
オプションの処理をするoption関数を定義する。
</p>

<pre><code>option :: [String] -&gt; Either String ([Switch], [String])</code></pre>

<p>
コマンドライン引数を処理し、エラーが無ければ
Right ([オプションのリスト], [オプション以外の引数])を返し、
エラーが生じた場合にはエラー文字列をすべて結合した文字列を返すようにする。
</p>

<pre><code>option args = case getOpt RequireOrder [
		Option ['0'] ["off"] (NoArg Off) "swith off",
		Option ['1'] ["on"] (NoArg On) "swith on" ]
	args of
	(ss, as, []) -&gt; Right (ss, as)
	(_, _, es) -&gt; Left $ concat es</code></pre>

<h3>main関数の定義</h3>

<pre><code>main = do
	args &lt;- getArgs
	case option args of
		Right (ss, as) -&gt; putStrLn $
			"OPTIONS: " ++ show ss ++ "\n" ++
			"ARGS   : " ++ show as ++ "\n"</code></pre>

<h3>試してみる</h3>

<pre><code>% runhaskell testOpt.hs -1100 --on --off hige --on
OPTIONS: [On, On, Off, Off, On, Off]
ARGS   : ["hige", "--on"]
% runhaskell testOpt.hs -1100 -1021 -123 --hoge --of hige --onn
unrecognized option `-2'
unrecognized option `-2'
unrecognized option `-3'
unrecognized option `--hoge'
% runhaskell testOpt.hs --off --of --on
OPTIONS: [Off, Off, On]
ARGS   : []
% runhaskell testOpt.hs --o
option `--o' is umbigous; could be one of:
  -0  --off  switch off
  -1  --on   switch on</code></pre>

<h2>冗長なコピーコマンド: 引数を持つオプションの例</h2>

<p><a href="/computer/haskell/standard/getopt/copy.hs"
	>copy.hs</a></p>

<h3>仕様</h3>

<p>以下のように実行するコピーコマンドを作成する。</p>

<pre><code>copy --infile foo.txt --outfile bar.txt</code></pre>
<pre><code>copy -i foo.txt -o bar.txt</code></pre>

<p>
裸のコマンドライン引数があったらエラーとする。
また入力ファイル、出力ファイルともに、最初に指定したもの以外は無視する。
</p>

<h3>Infile, Outfileデータ型の作成</h3>

<p>
オプションとして取得するためのデータ型を定義する。
</p>

<pre><code>data IOFile = Infile FilePath | Outfile FilePath</code></pre>

<p>
リストからInfileやOutfileを抽出するためにチェック関数を作成する。
</p>

<pre><code>isInfile, isOutfile :: IOFile -&gt; Bool
isInfile (Infile _) = True; isInfile _ = False
isOutfile (Outfile _) = True; isOutfile _ = False</code></pre>

<h3>コピー関数の作成</h3>

<p><a href="https://downloads.haskell.org/~ghc/latest/docs/html/libraries/directory-1.2.1.0/System-Directory.html"
	>System.Directory</a>を参照。</p>

<pre><code>copy :: IOFile -&gt; IOFile -&gt; IO ()
copy (Infile i) (Outfile o) = copyFile i o
copy _ _ = error "You must copy from Infile to Outfile."</code></pre>

<p>InfileからOutfileではないパターンはエラーとする。</p>

<h3>オプションの解析</h3>

<p>
オプションを解析する関数を作成する。
</p>

<pre><code>option :: [String] -&gt; Either String [IOFile]
option args = case getOpt RequireOrder [
		Option ['i'] ["infile"]
			(ReqArg Infile "file path")
			"input file",
		Option ['o'] ["outfile"]
			(ReqArg Outfile "file path")
			"output file"
	args of
	(ss, [], []) -&gt; Right ss
	(_, as, []) -&gt; Left $
		"Naked arguments: " ++ unwords as
	(_, _, es) -&gt; Left $ concat es</code></pre>

<p>
ReqArgの引数としてInfileやOutfileを指定している。
これによって返り値はInfile [ファイル名]やOutfile [ファイル名]のようになる。
</p>

<h3>リストから入力ファイル、出力ファイルを抽出</h3>

<p>補助関数としてMaybe型をEither型に変換する関数を定義しておく。</p>

<pre><code>maybeToEither :: String -&gt; Maybe a -&gt; Either String a
maybeToEither _ (Just x) = Right x
maybeToEither msg _ = Left msg</code></pre>

<p>IOFileのリストからInfileとOutfileをひとつずつ抽出する。</p>

<pre><code>getIOFiles :: [IOFile] -&gt; Either String (IOFile, IOFile)
getIOFiles iofs = (,)
	&lt;$&gt; maybeToEither
		"No input files" (find isInfile iofs)
	&lt;*&gt; maybeToEither
		"No output files" (find isOutfile iofs)</code></pre>

<h3>main関数</h3>

<pre><code>main = do
	args &lt;- getArgs
	either putStrLn (uncurry copy) $
		option args &gt;&gt;= getIOFiles</code></pre>

<p>
コマンドライン引数のオプションを処理し、入力用、出力用のファイル名を抽出する。
オプションからそれらが抽出できなかった場合には、
エラーメッセージを表示し、そうでなければcopy関数を適用する。
</p>

<h3>試してみる</h3>

<pre><code>% echo 'Hello, world!' &gt; hello.txt
% runhaskell copy.hs -i hello.txt -o copied.txt
% cat copied.txt
Hello, world!</code></pre>

<p>
<a href="/computer/haskell/standard/getopt/document.html"
	>「ドキュメント」へもどる</a>
<a href="/computer/haskell/standard/getopt"
	>「GetOptモジュール」トップへ</a>
</p>
